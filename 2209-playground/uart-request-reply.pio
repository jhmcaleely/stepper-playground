; A PIO program that performs a request-reply transaction on a bidirectional UART.

.pio_version 0 // only requires PIO version 0

.program uart_request_reply
.side_set 1 opt

; An 8n1 UART write then read transaction on one pin.
; OUT pin 0 and side-set pin 0 are both mapped to UART pin.
; IN pin 0 is the same pin.

    set y, 3
writeloop:
    pull       side 1 [7]  ; Assert stop bit, or stall with line in idle state
    set x, 7   side 0 [7]  ; Preload bit counter, assert start bit for 8 clocks
bitloopw:                   ; This loop will run 8 times (8n1 UART)
    out pins, 1            ; Shift 1 bit from OSR to the first OUT pin
    jmp x-- bitloopw   [6]  ; Each loop iteration is 8 cycles.
    jmp y-- writeloop

    set y, 7
readloop:
    wait 0 pin 0        ; Wait for start bit
    set x, 7 [7]       ; Preload bit counter
    nop [1]             ; delay until eye of first data bit
bitloopr:                ; Loop 8 times
    in pins, 1          ; Sample data
    jmp x-- bitloopr [6] ; Each iteration is 8 cycles
    jmp y-- readloop
